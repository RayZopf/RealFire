///////////////////////////////////////////////////////////////////////////////////////////////////
//Module check for needed files
//
//by: Zopf Resident - Ray Zopf (Raz)
//02. Feb 2014
//v0.2

//Files:
// PrintStatusInfo.lslm
//
//
//Prequisites: ----
//
//
//Changelog
// added perm check (needed for prim rezzing)

//FIXME: ----

//TODO: improve file check
//TODO: make file check (lists) the other way round: check if every inventory file is member of RealFire file list?
//
///////////////////////////////////////////////////////////////////////////////////////////////////


$module (integer m_iDebugMode, string m_sScriptName, integer m_iInvType, integer m_iFileStartAvail, string m_sTitle, integer m_iNFilesAvail, integer m_iAvail)

$import Debug.lslm(m_iDebugMode=m_iDebugMode, m_sScriptName=m_sScriptName);


//###
//CheckForFiles.lslm
//0.2 - 02Feb2014

string CheckForFiles(integer iNFiles, list lgivenFileList, integer iPermCheck, string sCurrentFile)
{
	integer iFileNumber = llGetInventoryNumber(m_iInvType);
	Debug("File number = " +(string)iFileNumber);
	if ( iFileNumber > 0) {
		m_iNFilesAvail = 0;
		list lFileAvail = [];
		list lFileList = [];
		integer i;
		for (i = 0; i < iFileNumber; ++i) { //assuming there are no interfering sources (scripts) with files in this prim!
			lFileList += llGetInventoryName(m_iInvType, i);
		}
		list lFileCompare = [];
		for (i = 0; i < iNFiles; ++i) {
			lFileCompare = llList2List(lgivenFileList, i, i);
			if (ERR_GENERIC == llListFindList(lFileList, lFileCompare)) {
				lFileAvail += FALSE;
				if (0 < i && (string)lFileCompare == sCurrentFile && 2 < iNFiles) {
					integer iFileNAvail = llGetListLength(lFileAvail);
					if (iNFiles > iFileNAvail) sCurrentFile = (string)llList2List(lgivenFileList, i+1, i+1);
						else {
							list lFileAvailTmp = llList2List(lFileAvail, 1, iNFiles-1);
							integer j = llListFindList(lFileAvailTmp, [TRUE]);
							if (0 <= j) sCurrentFile = (string)llList2List(lFileAvailTmp, j, j);
						}
				}
				llWhisper(0, m_sTitle+" - File not found in inventory: " + (string)lFileCompare);
			} else {
				if (iPermCheck) {
					if (!(PERM_COPY & llGetInventoryPermMask((string)lFileCompare, MASK_OWNER))) {
						llWhisper(0, m_sTitle+" - File has wrong permission: " + (string)lFileCompare);
						lFileAvail += FALSE;
					} else {
						lFileAvail += TRUE;
						m_iNFilesAvail++;
					}
				} else {
					lFileAvail += TRUE;
					m_iNFilesAvail++;
				}
			}
		}
		if (0 == llListFindList(lFileAvail, [TRUE])) m_iFileStartAvail = TRUE;
			else m_iFileStartAvail = FALSE;
		if (ERR_GENERIC != llListFindList(llList2List(lFileAvail, 1, iNFiles-1), [TRUE])) m_iAvail = TRUE;
			else m_iAvail = FALSE;
		return sCurrentFile;
	} else {
		m_iAvail = FALSE;
		return "0";
	}
}

//END CheckForFiles.lslm
//###